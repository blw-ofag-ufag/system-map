# TABLE OF OFFICES, SUBORGANIZATIONS AND INFORMATION THEY HAVE ACCESS TO

PREFIX schema: <http://schema.org/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX systemmap: <https://agriculture.ld.admin.ch/system-map/>

SELECT ?toplevelName ?organizationName ?informationName
WHERE {
  GRAPH <https://lindas.admin.ch/foag/system-map> {
    ?organization a systemmap:FederalOrganization .
    ?organization schema:name ?organizationName .
    FILTER(LANG(?organizationName)="de")
    ?organization systemmap:access ?information .
    ?information schema:name ?informationName .
    FILTER(LANG(?informationName)="de")
    ?organization schema:parentOrganization* ?toplevel .
    ?toplevel schema:name ?toplevelName .
    FILTER(LANG(?toplevelName)="de")
    FILTER NOT EXISTS {
  	  ?toplevel schema:parentOrganization ?x .
  	}
  }
}
